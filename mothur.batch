# Put this file in the working directory, run747, with the silva database and trainsets.
# Process paired-end reads, screen, cluster, and classify.
# Merge paired ends. 
#make.contigs(file=clado.files, processors=7)
# Subsample 10% (default) of the reads from each sample. 
#sub.sample(fasta=clado.trim.contigs.fasta, group=clado.contigs.groups)
# Sanity check. 
summary.seqs(fasta=current, count=current)
# Screen sequences with size limits that correspond to targeted regions (V3-V4) of 16S. 
screen.seqs(fasta=current, group=current, maxambig=0, maxlength=475)
# Sanity check. 
summary.seqs(fasta=current, count=current)
# Boil contigs down to just the unique sequences. 
unique.seqs()
# Sanity check.	
summary.seqs(fasta=current, count=current)
# Count resulting unique sequences to generate a table of the number of each in each sample. 
count.seqs(name=current, group=current)
# Curate / trim the Silva database to just V3-V4 regions to save on computing. 
pcr.seqs(fasta=silva.bacteria/silva.bacteria.fasta, start=6334, end=25432, keepdots=F, processors=20)
# Rename the curated /trimmed silva database. 
system(mv silva.bacteria/silva.bacteria.pcr.fasta silva.bacteria/silva.v3v4.fasta)
# Align the sequences with new silva file. 
align.seqs(fasta=clado.trim.contigs.good.unique.fasta, reference=silva.bacteria/silva.v3v4.fasta)
# Screen sequences based on alignment. 
screen.seqs(fasta=current, count=current, start=54, end=18982, maxhomop=8)
# Remove consistent gaps in the alignment. 
filter.seqs(fasta=current, vertical=T, trump=.)
# Get unique sequences only, again. 
unique.seqs(fasta=current, count=current)
# Sanity check. 
summary.seqs(fasta=current, count=current)
# Split the sequences by group and sort them by abundance, identifying sequences that are within 2 bases of each other. These are merged.
pre.cluster(fasta=current, count=current, diffs=4)
# Sanity check.		   
summary.seqs(fasta=current, count=current)
# Check for chimeras and remove. 
chimera.uchime(fasta=current, count=current, dereplicate=t)
remove.seqs(fasta=current, accnos=current)
# Sanity check.		   
summary.seqs(fasta=current, count=current)
classify.seqs(fasta=current, count=current, reference=trainset9_032012.pds.fasta, taxonomy=trainset9_032012.pds.tax, cutoff=80)
remove.lineage(fasta=current, count=current, taxonomy=current, taxon=Chloroplast-Mitochondria-unknown-Archaea-Eukaryota)
cluster.split(fasta=current, count=current, taxonomy=current, splitmethod=classify, taxlevel=4, cutoff=0.15)
make.shared(list=current, count=current, label=0.03)
classify.otu(list=current, count=current, taxonomy=current, label=0.03)
phylotype(taxonomy=current)
make.shared(list=current, count=current, label=1)
classify.otu(list=current, count=current, taxonomy=current, label=1)
